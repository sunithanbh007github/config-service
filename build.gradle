plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

description = 'Provides functionality for centralizing the application configuration.'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

ext {
	set('otelVersion', "1.17.0")
	set('springCloudVersion', "2022.0.3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-config-server'

	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelVersion}"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named("bootBuildImage") {
	environment = [
			//'HTTP_PROXY' : 'http.docker.internal:3128',
			//'HTTPS_PROXY' : 'http.docker.internal:3128',
			'BP_JVM_VERSION' : '17.*'
	]
	imageName = "${project.name}"
	docker {
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
